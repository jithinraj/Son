value ::= object | array | string | number | boolean | "null"

boolean ::= "true" | "false"

object ::= "{" ( member ( "," member )* )? "}"
member ::= string ":" value

array ::= "[" ( value ( "," value )* )? "]"

number ::= "-"? (positiveInteger fraction? | "0" fraction)
         | "0"

fraction        ::= "." digit* nonZeroDigit
positiveInteger ::= nonZeroDigit digit*

digit        ::= [#x30 - #x39]
nonZeroDigit ::= [#x31 - #x39]

string ::= '"'
           ( unescaped
           | "\" (shortcutEscape | codepointEscape)
           )*
           '"'
unescaped ::= ( [#x20 - #x21]
              | [#x23 - #x5B]
              | [#x5D - #x10FFFF]
              )
shortcutEscape ::= '"'
                 | "\"
                 | "b"
                 | "t"
                 | "n"
                 | "f"
                 | "r"
codepointEscape ::= "u00" ( "0" (([#x0 - #x7] | #xB) | [#xE - #xF])
                          | [#x10 - #x1F]
                          )
